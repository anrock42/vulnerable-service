import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id "org.springframework.boot" version "2.1.9.RELEASE" apply false
}

allprojects {
    apply plugin: 'io.spring.dependency-management'
//    apply plugin: "com.softwaresecured.reshift"

    repositories {
        mavenCentral()
        jcenter()
        jcenter {
            url "http://oss.jfrog.org/artifactory/oss-snapshot-local/"
            mavenContent {
                snapshotsOnly()
            }
        }
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencyManagement {
        overriddenByDependencies = false

        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom 'org.testcontainers:testcontainers-bom:1.12.4'
        }

        dependencies {
            dependency 'io.sentry:sentry-logback:1.7.13'
            dependencySet(group: 'io.springfox', version: '2.9.2') {
                entry 'springfox-swagger-ui'
                entry 'springfox-swagger2'
            }
        }
    }
}

subprojects {
    apply plugin:'java'

    targetCompatibility = sourceCompatibility = 11

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }
}

