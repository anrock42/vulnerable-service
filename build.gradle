import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        /*
        ./gradlew build reshiftAnalyse reshiftUpload -Dreshift.token=yKbOb6Tl6tIRRYFP0guN6CB1WSlSflPCnZOqUuPfgu4=
         */
        classpath "com.softwaresecured.reshift:gradle-plugin:1.+"
    }
}

plugins {
    id "org.springframework.boot" version "2.1.9.RELEASE" apply false
    id "org.sonarqube" version "2.8"
}

sonarqube {
    properties {
        property "sonar.projectKey", "anrock42_vulnerable-service"
        property "sonar.organization", "anrock42"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", "375e8248e05fdf341c31f7457ce5300d2887a313"
    }
}

allprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "com.softwaresecured.reshift"

    repositories {
        mavenCentral()
        jcenter()
        jcenter {
            url "http://oss.jfrog.org/artifactory/oss-snapshot-local/"
            mavenContent {
                snapshotsOnly()
            }
        }
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencyManagement {
        overriddenByDependencies = false

        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom 'org.testcontainers:testcontainers-bom:1.12.4'
        }

        dependencies {
            dependency 'io.sentry:sentry-logback:1.7.13'
            dependencySet(group: 'io.springfox', version: '2.9.2') {
                entry 'springfox-swagger-ui'
                entry 'springfox-swagger2'
            }
        }
    }
}

subprojects {
    apply plugin:'java'

    targetCompatibility = sourceCompatibility = 11

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }
}

